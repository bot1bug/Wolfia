/*
 * Copyright (C) 2016-2020 the original author or authors
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */


import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage
import org.sonarqube.gradle.SonarQubeTask
import space.npstr.gradle.docker.DockerWaitHealthyContainer

buildscript {
    apply from: 'gradle/versions.gradle'
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "gradle.plugin.org.flywaydb:gradle-plugin-publishing:$flywayVersion"

        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:$gradleGitVersion"
        classpath "org.ajoberstar.grgit:grgit-gradle:$grGitVersion"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonarVersion"
        classpath "nu.studer:gradle-jooq-plugin:$jooqPluginVersion"
        classpath "org.jooq:jooq-codegen:$jooqVersion"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versionsPluginVersion"
        classpath "com.bmuschko:gradle-docker-plugin:$dockerPluginVersion"
        classpath "com.adarshr:gradle-test-logger-plugin:$testLoggerVersion"
        classpath "org.siouan:frontend-gradle-plugin-jdk11:$frontendPluginVersion"
    }
}

ext {
    buildNumber = (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'DEV')
    isProd = project.hasProperty('prod')
}

configure(allprojects.findAll { it.name != 'platform' }) {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    //set up autogenerated files in idea
    compileJava {
        options.annotationProcessorPath = configurations.annotationProcessor
    }
    idea {
        module {
            sourceDirs += sourceSets.main.output.generatedSourcesDirs
            generatedSourceDirs += sourceSets.main.output.generatedSourcesDirs
            testSourceDirs += sourceSets.test.output.generatedSourcesDirs
            generatedSourceDirs += sourceSets.test.output.generatedSourcesDirs
        }
    }

    dependencies {
        api platform(project(':platform'))
        annotationProcessor platform(project(':platform'))
        compileOnly platform(project(':platform'))
    }
}

apply plugin: 'com.github.ben-manes.versions'

dependencyUpdates {
    checkConstraints = true
    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'm1', 'm2', 'm3', 'm4', 'm5', 'preview', 'pr1', 'pr2', 'pr3',
                                    'b180830.0438', 'b180830.0359', 'b180725.0644', 'b180725.0427', 'b01', // jaxb bullshit
                ].any {
                    q -> selection.candidate.version.toLowerCase().contains(q)
                }
                if (rejected) {
                    selection.reject('Alpha or beta version')
                }
            }
        }
    }
}

allprojects {
    apply plugin: 'org.ajoberstar.grgit'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'jacoco'

    group = 'space.npstr.wolfia'
    version = "${versionTag()}".toString()

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
            vendor = JvmVendorSpec.ADOPTOPENJDK
            // implementation = JvmImplementation.J9 seems to break CI builds due to different nano second resolution
        }
        consistentResolution {
            useCompileClasspathVersions()
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://m2.dv8tion.net/releases'
            content { includeModule("net.dv8tion", "JDA") }
            content { includeGroup("club.minnced") }
        }
        maven {
            url 'https://jitpack.io'
            content { includeGroup("space.npstr") }
        }
    }

    test {
        useJUnitPlatform()
        jvmArgs '-XX:TieredStopAtLevel=1'
        filter {
            if (project.hasProperty('excludeUiTests')) {
                excludeTestsMatching "*UiTest"
            }
        }
    }

    testlogger {
        theme 'mocha-parallel'
        showFullStackTraces true
    }

    jacocoTestReport {
        reports {
            xml.required.set(true)
        }
    }

    defaultTasks 'build'
}

project(':platform') {
    apply plugin: 'java-platform'
    javaPlatform {
        allowDependencies()
    }
    dependencies {
        api platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        api platform("io.prometheus:simpleclient_bom:$prometheusVersion")
        constraints {
            api "net.dv8tion:JDA:$jdaVersion"
            api "club.minnced:discord-webhooks:$discordWebhooksVersion"
            api "io.sentry:sentry-logback:$sentryLogbackVersion"
            api "org.json:json:$orgJsonVersion"
            api "space.npstr:annotations:$annotationsVersion"
            api "org.immutables:value:$immutablesVersion"
            api "com.sun.xml.bind:jaxb-impl:$jaxbImplVersion"
            api "org.glassfish.jaxb:jaxb-core:$jaxbImplVersion"
            api "org.glassfish.jaxb:jaxb-runtime:$jaxbImplVersion"

            api "space.npstr:prometheus_extensions:$promExtVersion"

            api "net.ttddyy:datasource-proxy:$dsProxyVersion"
            api "org.flywaydb:flyway-core:$flywayVersion"
            api "org.jooq:jooq:$jooqVersion"
            api "org.jooq:jooq-meta:$jooqVersion"
            api "org.jooq:jooq-codegen:$jooqVersion"

            api "org.togglz:togglz-core:$togglzVersion"
            api "org.togglz:togglz-slf4j:$togglzVersion"
            api "org.togglz:togglz-spring-boot-starter:$togglzVersion"
            api "org.togglz:togglz-console:$togglzVersion"
            api "org.togglz:togglz-spring-security:$togglzVersion"

            api "org.testcontainers:testcontainers:$testcontainersVersion"
            api "org.testcontainers:selenium:$testcontainersVersion"
            api "com.codeborne:selenide:$selenideVersion"
        }
    }
}

project(':database-codegen') {
    apply plugin: 'org.flywaydb.flyway'
    apply plugin: 'nu.studer.jooq'
    apply plugin: 'com.bmuschko.docker-remote-api'

    configurations {
        flywayMigration
    }

    ext {
        codegenDbImageRepo = 'napstr/poggres'
        codegenDbImageTag = '14'
        codegenDbImageName = codegenDbImageRepo + ':' + codegenDbImageTag
        codegenDbContainerName = 'wolfia-codegen-postgres'
        codegenJdbcUrl = "jdbc:postgresql://127.0.0.1:5435/codegen?user=codegen"
    }

    dependencies {
        flywayMigration platform(project(':platform'))
        jooqGenerator platform(project(':platform'))

        api "org.jooq:jooq"
        implementation "org.jooq:jooq-meta"
        implementation "org.jooq:jooq-codegen"
        api "org.postgresql:postgresql"
        flywayMigration "org.postgresql:postgresql"
        jooqGenerator "org.postgresql:postgresql"
    }

    task pullCodegenDbContainer(type: DockerPullImage) {
        image.set(codegenDbImageName)
    }

    task createCodegenDbContainer(type: DockerCreateContainer) {
        dependsOn pullCodegenDbContainer
        targetImageId(codegenDbImageName)
        containerName.set(codegenDbContainerName)
        hostConfig.autoRemove.set(true)
        hostConfig.portBindings.set(['127.0.0.1:5435:5432'])
        withEnvVar('POSTGRES_HOST_AUTH_METHOD', 'trust')
        withEnvVar('ROLE', 'codegen')
        withEnvVar('DB', 'codegen')
        withEnvVar('EXTENSIONS', 'hstore')
    }

    task stopCodegenDbContainerPre(type: StopCodegenDbContainer) {
        targetContainerId codegenDbContainerName
    }

    task stopCodegenDbContainerPost(type: StopCodegenDbContainer) {
        targetContainerId codegenDbContainerName
    }


    task startCodegenDbContainer(type: DockerStartContainer) {
        createCodegenDbContainer.mustRunAfter stopCodegenDbContainerPre
        dependsOn(stopCodegenDbContainerPre, createCodegenDbContainer)
        targetContainerId codegenDbContainerName
    }

    task awaitCodegenDbContainer(type: DockerWaitHealthyContainer) {
        dependsOn startCodegenDbContainer
        targetContainerId codegenDbContainerName
        awaitStatusTimeout.set(40)
    }

    def migrationsInput = fileTree('src/main/resources/db/migrations')
    def migrationsOutput = fileTree('build/classes/generated/java/space/npstr/wolfia/db/gen')

    flyway {
        tasks.withType(org.flywaydb.gradle.task.AbstractFlywayTask) {
            inputs.files(migrationsInput)
                    .withPropertyName("flywayMigrationsInput")
                    .withPathSensitivity(PathSensitivity.RELATIVE)
            outputs.files(migrationsOutput)
                    .withPropertyName("flywayMigrationsOutput")
        }
        flywayMigrate {
            doFirst {
                // running these manually in a doFirst closure ensures that they are not executed all the time
                // currently there is no better way of running tasks optionally that another task depends on
                pullCodegenDbContainer.start()
                stopCodegenDbContainerPre.start()
                createCodegenDbContainer.start()
                startCodegenDbContainer.start()
                awaitCodegenDbContainer.start()
            }
        }

        url = "$codegenJdbcUrl"
        locations = ['filesystem:src/main/resources/db/migrations']
        configurations = ['flywayMigration']
    }

    jooq {
        version = jooqVersion
        edition = nu.studer.gradle.jooq.JooqEdition.OSS
        configurations {
            main {
                generationTool {
                    logging = org.jooq.meta.jaxb.Logging.WARN
                    jdbc {
                        driver = 'org.postgresql.Driver'
                        url = "$codegenJdbcUrl"
                    }
                    generator {
                        name = 'org.jooq.codegen.DefaultGenerator'
                        strategy {
                            name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                        }
                        database {
                            name = 'org.jooq.meta.postgres.PostgresDatabase'
                            inputSchema = 'public'
                            forcedTypes {
                                forcedType {
                                    userType = 'space.npstr.wolfia.db.type.OAuth2Scope[]'
                                    converter = 'space.npstr.wolfia.db.converter.OAuth2ScopeEnumArrayConverter'
                                    includeExpression = '.*'
                                    includeTypes = '_OAuth2Scope'
                                }
                                forcedType {
                                    userType = 'java.util.HashMap<java.lang.String, java.lang.String>'
                                    binding = 'space.npstr.wolfia.db.PostgresHstoreBinding'
                                    includeExpression = '.*'
                                    includeTypes = 'hstore'
                                }
                                forcedType {
                                    userType = 'java.time.Instant'
                                    converter = '''
                                org.jooq.Converter.ofNullable(
                                java.time.OffsetDateTime.class,
                                java.time.Instant.class,
                                o -> o.toInstant(),
                                i -> i.atOffset(java.time.ZoneOffset.UTC))
                             '''
                                    includeTypes = 'timestamp\\ with\\ time\\ zone'
                                }
                                forcedType {
                                    userType = 'java.net.URI'
                                    converter = 'space.npstr.wolfia.db.converter.UriConverter'
                                    includeExpression = '.*link'
                                    includeTypes = 'text'
                                }
                            }
                        }
                        target {
                            packageName = 'space.npstr.wolfia.db.gen'
                            directory = 'build/classes/generated/java'
                        }
                    }
                }
            }
        }
    }

    generateJooq {
        allInputsDeclared.set(true)
        inputs.files(migrationsInput)
                .withPropertyName('jooqMigrationsInput')
                .withPathSensitivity(PathSensitivity.RELATIVE)
        outputs.files(migrationsOutput)
                .withPropertyName('jooqMigrationsOutput')
        dependsOn(flywayMigrate)
        doLast {
            stopCodegenDbContainerPost.start()
        }
    }

    test {
        dependsOn(flywayMigrate)
        doLast {
            stopCodegenDbContainerPost.start()
        }
    }
}

project(':common') {
    dependencies {
        api "org.slf4j:slf4j-api"
    }
}

project(':frontend') {
    apply plugin: 'org.siouan.frontend-jdk11'

    frontend {
        nodeVersion = "$theNodeVersion"
        yarnEnabled = true
        yarnVersion = "$theYarnVersion"
        assembleScript = rootProject.ext.isProd ? 'run build' : 'run build --mode development'
        checkScript = 'run lint --no-fix --max-errors 0 --max-warnings 0'
    }

    def frontendBuildCacheConfig = {
        inputs.files('package.json', 'yarn.lock', 'babel.config.js', 'vue.config.js')
                .withPropertyName('frontendConfigFiles')
                .withPathSensitivity(PathSensitivity.RELATIVE)
        inputs.dir('src')
                .withPropertyName('frontendSources')
                .withPathSensitivity(PathSensitivity.RELATIVE)
        inputs.dir('public')
                .withPropertyName('frontendPublicDir')
                .withPathSensitivity(PathSensitivity.RELATIVE)
    }

    tasks.named('assembleFrontend') {
        configure frontendBuildCacheConfig
        outputs.dir("$buildDir/dist")
                .withPropertyName('frontendOutput')
        outputs.cacheIf { true }
    }
    tasks.named('checkFrontend').configure frontendBuildCacheConfig

    tasks.register('setVersion', org.siouan.frontendgradleplugin.infrastructure.gradle.RunNpmYarn) {
        dependsOn tasks.named('installNode')
        script = "version --no-git-tag-version --new-version ${rootProject.version}"
    }
    tasks.named('assembleFrontend').configure { dependsOn 'setVersion' }
}

apply plugin: 'application'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.sonarqube'


mainClassName = 'space.npstr.wolfia.Launcher'

configurations {
    // fucks with spring boot jar, we dont need it anyways
    // be VERY careful and test the produced jar if ever reenabled
    compile.exclude module: 'opus-java'

    // undertow master race
    implementation.exclude module: 'spring-boot-starter-reactor-netty'
    implementation.exclude module: 'spring-boot-starter-tomcat'
}

dependencies {
    implementation project(':common')
    implementation project(':database-codegen')

    implementation "net.dv8tion:JDA"
    implementation "club.minnced:discord-webhooks"

    implementation "ch.qos.logback:logback-classic"
    implementation "io.sentry:sentry-logback"

    implementation "org.yaml:snakeyaml"
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.github.ben-manes.caffeine:caffeine"
    implementation "org.json:json"
    implementation "space.npstr:annotations"

    implementation "io.lettuce:lettuce-core"
    implementation(group: "io.netty", name: "netty-transport-native-epoll", classifier: "linux-x86_64")
    implementation(group: "io.netty", name: "netty-transport-native-kqueue", classifier: "osx-x86_64")

    implementation "io.prometheus:simpleclient"
    implementation "io.prometheus:simpleclient_hotspot"
    implementation "io.prometheus:simpleclient_logback"
    implementation "io.prometheus:simpleclient_common"
    implementation "io.prometheus:simpleclient_caffeine"
    implementation "space.npstr:prometheus_extensions"

    annotationProcessor "org.immutables:value"
    compileOnly(group: 'org.immutables', name: 'value', classifier: 'annotations')

    implementation "net.ttddyy:datasource-proxy"
    implementation "org.flywaydb:flyway-core"

    implementation "org.togglz:togglz-core"
    implementation "org.togglz:togglz-slf4j"
    implementation "org.togglz:togglz-spring-boot-starter"
    implementation "org.togglz:togglz-console"
    implementation "org.togglz:togglz-spring-security"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-undertow"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.session:spring-session-data-redis"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    compileOnly("org.springframework.boot:spring-boot-devtools")

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude module: 'android-json' // json.org clone
    }
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:selenium"
    testImplementation "com.codeborne:selenide"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-inline" // to mock final classes
    testImplementation "org.awaitility:awaitility"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation' << '-Xmaxerrs' << '10000' << '-Xdiags:verbose'
}

//required by spring boot configuration processor
compileJava.dependsOn(processResources)

processResources {
    dependsOn ':frontend:assembleFrontend'

    from("${project(':frontend').buildDir}/dist") {
        into 'static'
    }
}

tasks.withType(SonarQubeTask) {
    dependsOn jacocoTestReport
}

springBoot {
    buildInfo {
        properties {
            time = null
        }
    }
}

bootRun {
    //compiling tests during bootRun increases the likelyhood of catching broken tests locally instead of on the CI
    dependsOn compileTestJava

    //pass in custom jvm args
    // source: https://stackoverflow.com/a/25079415
    // example: ./gradlew bootRun -PjvmArgs="--illegal-access=debug -Dwhatever=value"
    if (project.hasProperty('jvmArgs')) {
        //noinspection GroovyAssignabilityCheck
        jvmArgs project.jvmArgs.split('\\s+')
    }
}

bootJar {
    archiveFileName = "wolfia.jar"
}

//need two of these tasks, otherwise gradle complains about circular dependencies
class StopCodegenDbContainer extends DockerStopContainer {
    StopCodegenDbContainer() {
        onError { exception ->
            // Ignore exception if container does not exist otherwise throw it
            if (exception != null && exception.message != null //memes
                    && !exception.message.contains('No such container'))
                throw exception
        }
    }
}

//returns the last git tag (that needs to be in the form of MAJOR.MINOR.PATCH) and the build number
//@SuppressWarnings("GrMethodMayBeStatic")
String versionTag() {
    def matcher = /^([0-9]+\.[0-9]+\.[0-9]+).*$/
    def match = ("${grgit.describe()}" =~ matcher)[0]

    //noinspection GroovyAssignabilityCheck
    def result = match[1]
    result += '-'
    result += project.ext.buildNumber

    println("Version: " + result)
    return result
}
